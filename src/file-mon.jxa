
(ns file-mon
    (require file timer io os lists string (erlang :as erl))
    (use (joxa-core :only (and/1 let/2 if/3))
	 (io_lib :only (format/2))
	 (flow-utils :only (multicast/2 wait-for-file/3))))


;; forward definitions
(defspec monitor-device ((erlang/string) (erlang/pid) (erlang/number) (erlang/number) (erlang/fun) (erlang/list)) (erlang/any))

  ;; check if more retries are available, if not, transmit :eof and stop
(defn monitor-exit? (file-name dev timeout-ms 
			retries-left done? targets)
  (if (erl/> retries-left 0)
      (do (timer/sleep timeout-ms)
	  (monitor-device file-name dev timeout-ms
			  (erl/- retries-left 1) (fn () :true) targets))
      (multicast :eof targets)))


;; monitors the open device for new lines, which are sent to a list of processes
(defn monitor-device (file-name dev timeout-ms retries-left done? targets)
  (case (file/read_line dev)
	({:ok line} (multicast {:line line} targets)
	            (monitor-device file-name dev timeout-ms retries-left done? targets))
	(:eof (if (done?)
		  (monitor-exit? file-name dev timeout-ms retries-left done? targets)
		(do (timer/sleep timeout-ms)
		      (monitor-device file-name dev timeout-ms
				      retries-left done? targets))))
	({:error reason} 
	 (multicast  {:failure (format "failed to watch file ~s with reason ~p" [file-name reason])}
		     targets))))


;; monitor the file file-name while checking whether writing to the file is considered done
;; using done? and transmitting lines/eof messages to all targets
;; when the file is closed, the monitor exits by itself
(defn+ monitor-file (file-name done? targets)
  ;; wait max 4 seconds for the file to appear in case it's visible immediately
  (case (wait-for-file file-name 4000 200)
	({:success _f} 
	 {:pid (erl/spawn (fn () (let ({:ok dev} (file/open file-name [:read]))
					   (monitor-device file-name dev 500 2 done? targets)
					   (file/close dev))))})
	(failure failure)))
